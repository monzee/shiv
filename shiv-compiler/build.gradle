plugins {
    id 'kotlin'
    id 'kotlin-kapt'
    id 'maven-publish'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.squareup:javapoet:1.11.1"
    implementation "com.google.auto:auto-common:0.10"
    implementation "com.google.auto.service:auto-service:1.0-rc6"
    kapt "com.google.auto.service:auto-service:1.0-rc6"
    testImplementation "com.google.testing.compile:compile-testing:0.18"
    testImplementation "com.google.truth:truth:1.0"
}

task jarSources(type: Jar, dependsOn: "classes", group: "publishing") {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    repositories.maven {
        name "bintray"
        url "https://api.bintray.com/maven/monzee/jvm/shiv-compiler/"
        credentials {
            username = BINTRAY_USER
            password = BINTRAY_API_KEY
        }
    }

    publications {
        compiler(MavenPublication) {
            from components.java
            artifact jarSources
            pom {
                name = "Shiv Compiler"
                description = "Annotation processor to generate dagger2 modules used by shiv-runtime"
                developers {
                    developer {
                        id = "monzee"
                        name = "Mon Zafra"
                        email = "mz@codeia.ph"
                        organization = "Codeia Inc."
                        organizationUrl = "https://codeia.ph"
                    }
                }
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://mit-license.org/license.txt"
                        distribution = "repo"
                    }
                }
                scm {
                    url = "https://github.com/monzee/shiv"
                    connection = "scm:git:https://github.com/monzee/shiv.git"
                    developerConnection = "scm:git:ssh://git@github.com:monzee/shiv.git"
                }
            }
        }
    }
}
